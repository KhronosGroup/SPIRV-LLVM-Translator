; REQUIRES: spirv-as
; RUN: spirv-as --target-env spv1.0 -o %t.spv %s
; RUN: spirv-val %t.spv
; RUN: llvm-spirv -r -o - %t.spv | llvm-dis | FileCheck %s
               OpCapability Addresses
               OpCapability Kernel
               OpMemoryModel Physical32 OpenCL
               OpEntryPoint Kernel %1 "testFNegate"
               OpName %a "a"
               OpName %r1 "r1"
               OpName %r2 "r2"
               OpName %r3 "r3"
               OpName %r4 "r4"
               OpName %r5 "r5"
               OpName %r6 "r6"
               OpName %r7 "r7"
               OpName %r8 "r8"
               OpName %r9 "r9"
               OpDecorate %r2 FPFastMathMode None
               OpDecorate %r3 FPFastMathMode NotNaN
               OpDecorate %r4 FPFastMathMode NotInf
               OpDecorate %r5 FPFastMathMode NSZ
               OpDecorate %r6 FPFastMathMode AllowRecip
               OpDecorate %r7 FPFastMathMode Fast
               OpDecorate %r8 FPFastMathMode NotNaN|NotInf
               OpDecorate %r9 FPFastMathMode NotNaN|Fast
       %void = OpTypeVoid
      %float = OpTypeFloat 32
          %5 = OpTypeFunction %void %float
          %1 = OpFunction %void None %5
          %a = OpFunctionParameter %float
          %6 = OpLabel
         %r1 = OpFNegate %float %a
         %r2 = OpFNegate %float %a
         %r3 = OpFNegate %float %a
         %r4 = OpFNegate %float %a
         %r5 = OpFNegate %float %a
         %r6 = OpFNegate %float %a
         %r7 = OpFNegate %float %a
         %r8 = OpFNegate %float %a
         %r9 = OpFNegate %float %a
               OpReturn
               OpFunctionEnd

; CHECK: %r1 = fneg float %a
; CHECK: %r2 = fneg float %a
; CHECK: %r3 = fneg nnan float %a
; CHECK: %r4 = fneg ninf float %a
; CHECK: %r5 = fneg nsz float %a
; CHECK: %r6 = fneg arcp float %a
; CHECK: %r7 = fneg fast float %a
; CHECK: %r8 = fneg nnan ninf float %a
; CHECK: %r9 = fneg fast float %a
